apply plugin: 'com.android.application'

def buildFirebase = project.hasProperty('buildFirebase')

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 2
        multiDexEnabled true
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters "armeabi-v7a", "x86", "armeabi", "mips"
        }
    }

    sourceSets {
        main {
            aidl.srcDirs = ['src/main/java']

//            jniLibs.srcDir 'src/main/cpp'
            jni.srcDirs = []
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "push"
    productFlavors {
        noPush {
            dimension "push"
        }
        withFirebase {
            dimension "push"
        }
    }
//    signingConfigs {
//        config {
//            keyAlias 'ring'
//            storeFile file('../keystore.bin')
//        }
//    }
    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds false
        abortOnError false
    }
//    splits {
//        abi {
//            enable true
//            reset()
//            def sp = archs.split(',')
//            include(sp)
//            universalApk true
//        }
//    }
}

dependencies {
    def butterknife_version = "10.0.0"
    def dagger_version = "2.12"

    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation project(':libringclient')

    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // https://mvnrepository.com/artifact/com.googlecode.ez-vcard/ez-vcard
    implementation group: 'com.googlecode.ez-vcard', name: 'ez-vcard', version: '0.10.2'


    def leanback_version = "1.1.0-alpha03"

    implementation "androidx.leanback:leanback:$leanback_version"
    implementation "androidx.leanback:leanback-preference:$leanback_version"

    // ORM
    implementation 'com.j256.ormlite:ormlite-android:4.48'

    // Barcode scanning
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0@aar'

    // Butterknife
    implementation "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

    implementation 'com.skyfishjy.ripplebackground:library:1.0.1'

    // Dagger dependency injection
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:3.8.0'

    // RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    withFirebaseImplementation 'com.google.firebase:firebase-messaging:11.8.0'
}

if (buildFirebase) {
    println 'apply plugin ' + buildFirebase
    apply plugin: 'com.google.gms.google-services'
}